<html>

<head>
<title>Toon-Phong Demo</title>
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

<script type="text/javascript" src="glMatrix-0.9.5.min.js"></script>
<script type="text/javascript" src="webgl-utils.js"></script>

<script id="skybox-fs" type="x-shader/x-fragment">
    precision mediump float;

    varying vec3 vTextureCoord;
    uniform samplerCube uSampler;

    void main(void) {
        gl_FragColor = textureCube(uSampler, vTextureCoord);
    }
</script>
<script id="skybox-vs" type="x-shader/x-vertex">
    attribute vec3 aVertexPosition;
    varying vec3 vTextureCoord;
    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;

    void main(void) {
        vTextureCoord = aVertexPosition;
        vTextureCoord.y = -vTextureCoord.y;
        gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
    }
</script>
<script id="toon-phong-fs" type="x-shader/x-fragment">
    precision mediump float;

    varying vec2 vTextureCoord;
    varying vec3 vTransformedNormal;
    varying vec4 vPosition;
    varying vec4 vSPosition;

    uniform float uMaterialShininess;

    uniform bool uShowSpecularHighlights;
    uniform bool uUseTextures;
    uniform float uSplit;

    uniform vec3 uAmbientColor;

    uniform vec3 uPointLightingLocation;
    uniform vec3 uPointLightingSpecularColor;
    uniform vec3 uPointLightingDiffuseColor;

    uniform sampler2D uSampler;


    void main(void) {
        vec3 lightWeighting;
        vec3 lightDirection = normalize(uPointLightingLocation - vPosition.xyz);
        vec3 normal = normalize(vTransformedNormal);

        float specularLightWeighting = 0.0;
        if (uShowSpecularHighlights) {
            vec3 eyeDirection = normalize(-vPosition.xyz);
            vec3 reflectionDirection = reflect(-lightDirection, normal);

            specularLightWeighting = pow(
                max(dot(reflectionDirection, eyeDirection), 0.0),
                uMaterialShininess);
        }

        float diffuseLightWeighting = max(dot(normal, lightDirection), 0.0);
        if (vSPosition.x / vSPosition.w > uSplit) {
            diffuseLightWeighting = 0.8 * smoothstep(.14,.16,diffuseLightWeighting);
            specularLightWeighting = 0.4 * smoothstep(.29,.31,specularLightWeighting);
        }
        lightWeighting = uAmbientColor
            + uPointLightingSpecularColor * specularLightWeighting
            + uPointLightingDiffuseColor * diffuseLightWeighting;

        vec4 fragmentColor;
        if (uUseTextures) {
            fragmentColor = texture2D(uSampler, vTextureCoord.st);
        } else {
            fragmentColor = vec4(1.0, 1.0, 1.0, 1.0);
        }
        gl_FragColor = vec4(fragmentColor.rgb * lightWeighting, fragmentColor.a);
    }
</script>

<script id="toon-phong-vs" type="x-shader/x-vertex">
    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;
    attribute vec2 aTextureCoord;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform mat3 uNMatrix;

    varying vec2 vTextureCoord;
    varying vec3 vTransformedNormal;
    varying vec4 vPosition;
    varying vec4 vSPosition;


    void main(void) {
        vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
        gl_Position = uPMatrix * vPosition;
        vSPosition = gl_Position;
        vTextureCoord = aTextureCoord;
        vTransformedNormal = uNMatrix * aVertexNormal;
    }
</script>

<script id="outline-fs" type="x-shader/x-fragment">
    precision mediump float;

    varying vec3 vTransformedNormal;
    varying vec4 vPosition;
    varying vec2 vTextureCoord;
    uniform sampler2D uSampler;

    void main(void) {
        vec4 color = texture2D(uSampler, vTextureCoord.st);
        gl_FragColor = vec4(color.rgb * 0.15, 1.0);
    }
</script>

<script id="outline-vs" type="x-shader/x-vertex">
    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;
    attribute vec2 aTextureCoord;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;
    uniform mat3 uNMatrix;

    varying vec3 vTransformedNormal;
    varying vec4 vPosition;
    varying vec2 vTextureCoord;

    void main(void) {
        float outline_weight = 0.03;
        vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
        vTextureCoord = aTextureCoord;
        vTransformedNormal = uNMatrix * aVertexNormal;
        vec2 outline = outline_weight * normalize(vTransformedNormal.xy);
        gl_Position = uPMatrix * vPosition + vec4(outline, 0.0, 0.0);
    }
</script>

<script type="text/javascript" src="webgl-obj-loader.js"></script>
<script type="text/javascript" src="globals.js"></script>
<script type="text/javascript" src="helpers.js"></script>
<script type="text/javascript" src="initialize.js"></script>
<script type="text/javascript" src="camera.js"></script>
<script type="text/javascript" src="myscene.js"></script>
<script type="text/javascript" src="webgl.js"></script>
</head>


<body>
    <canvas id="demo-canvas" style="border: none;" width="1024" height="576"></canvas>

    <br/>
    Click on window to enter fullscreen mode.
    <br/>
    <br/>

    <input type="checkbox" id="specular" checked /> Show specular highlight<br/>
    <input type="checkbox" id="outline" checked /> Show outlines<br/>
    <input type="range" id="split" min="0" max="100" value="50" /> Location of split<br/>
    <br/>


    <h2>Material:</h2>

    <table style="border: 0; padding: 10px;">
        <tr>
            <td><b>Shininess:</b>
            <td><input type="text" id="shininess" value="32.0" />
        </tr>
    </table>


    <h2>Point light:</h2>

    <table style="border: 0; padding: 10px;">
        <tr>
            <td><b>Location:</b>
            <td>X: <input type="text" id="lightPositionX" value="-20.0" />
            <td>Y: <input type="text" id="lightPositionY" value="40.0" />
            <td>Z: <input type="text" id="lightPositionZ" value="20.0" />
        </tr>
        <tr>
            <td><b>Specular colour:</b>
            <td>R: <input type="text" id="specularR" value="0.8" />
            <td>G: <input type="text" id="specularG" value="0.8" />
            <td>B: <input type="text" id="specularB" value="0.8" />
        </tr>
        <tr>
            <td><b>Diffuse colour:</b>
            <td>R: <input type="text" id="diffuseR" value="0.8" />
            <td>G: <input type="text" id="diffuseG" value="0.8" />
            <td>B: <input type="text" id="diffuseB" value="0.8" />
        </tr>
    </table>


    <h2>Ambient light:</h2>

    <table style="border: 0; padding: 10px;">
        <tr>
            <td><b>Colour:</b>
            <td>R: <input type="text" id="ambientR" value="0.2" />
            <td>G: <input type="text" id="ambientG" value="0.2" />
            <td>B: <input type="text" id="ambientB" value="0.2" />
        </tr>
    </table>
    <br/>
    Palm trees model by <a href="http://www.turbosquid.com/FullPreview/Index.cfm/ID/794449">fantazir</a>.
    Mouse model by <a href="http://tf3dm.com/3d-model/puo-29311.html">3dregenerator</a>.
    WebGL OBJ loader and camera controls by <a href="https://github.com/frenchtoast747/webgl-obj-loader">frenchtoast747</a>.
</body>

</html>

